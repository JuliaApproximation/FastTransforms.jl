# # Semi-classical Jacobi polynomials
# In this example, we will consider the semi-classical orthogonal polynomials with respect to the inner product:
# ```math
# \langle f, g \rangle = \int_{-1}^1 f(x) g(x) w(x){\rm d} x,
# ```
# where $w(x) = w^{(\alpha,\beta,\gamma,\delta,\epsilon)}(x) = (1-x)^\alpha(1+x)^\beta(2+x)^\gamma(3+x)^\delta(5-x)^\epsilon$ is a modification of the Jacobi weight.
# We shall use results from [this paper](https://arxiv.org/abs/2302.08448) to consider these semi-classical orthogonal polynomials as modifications of the orthonormalized Jacobi polynomials $\tilde{P}_n^{(\alpha,\beta)}(x)$.

using ApproxFun, FastTransforms, LazyArrays, LinearAlgebra, Plots, LaTeXStrings
const GENFIGS = joinpath(pkgdir(FastTransforms), "docs/src/generated")
!isdir(GENFIGS) && mkdir(GENFIGS)
plotlyjs()

# We set the five parameters:
Œ±,Œ≤,Œ≥,Œ¥,œµ = -0.125, -0.25, 0.123, 0.456, 0.789

# We use `ApproxFun` to construct a finite normalized Jacobi series as a proxy for $(2+x)^\gamma(3+x)^\delta(5-x)^\epsilon$.
u = Fun(x->(2+x)^Œ≥*(3+x)^Œ¥*(5-x)^œµ, NormalizedJacobi(Œ≤, Œ±))

# Our working polynomial degree will be:
n = 100

# We compute the connection coefficients between the modified orthogonal polynomials and the Jacobi polynomials:
P = plan_modifiedjac2jac(Float64, n+1, Œ±, Œ≤, u.coefficients)

# We store the connection to first kind Chebyshev polynomials:
P1 = plan_jac2cheb(Float64, n+1, Œ±, Œ≤; normjac = true)

# We compute the Chebyshev series for the degree-$k\le n$ modified polynomial and its values at the Chebyshev points:
q = k -> lmul!(P1, lmul!(P, [zeros(k); 1.0; zeros(n-k)]))
qvals = k -> ichebyshevtransform(q(k))

# With the symmetric Jacobi matrix for $\tilde{P}_n^{(\alpha, \beta)}(x)$ and the modified plan, we may compute the modified Jacobi matrix and the corresponding roots (as eigenvalues):
x = Fun(x->x, NormalizedJacobi(Œ≤, Œ±))
XP = SymTridiagonal(Symmetric(Multiplication(x, space(x))[1:n+1, 1:n+1]))
XQ = FastTransforms.modified_jacobi_matrix(P, XP)
view(XQ, 1:7, 1:7)

# And we plot:
x = chebyshevpoints(Float64, n+1, Val(1))
p = plot(x, qvals(0); linewidth=2.0, legend = false, xlim=(-1,1), xlabel=L"x",
         ylabel=L"Q_n(x)", title="Semi-classical Jacobi Polynomials and Their Roots",
         extra_plot_kwargs = KW(:include_mathjax => "cdn"))
for k in 1:10
    Œª = eigvals(SymTridiagonal(XQ.dv[1:k], XQ.ev[1:k-1]))
    plot!(x, qvals(k); linewidth=2.0, color=palette(:default)[k+1])
    scatter!(Œª, zero(Œª); markersize=2.5, color=palette(:default)[k+1])
end
p
savefig(joinpath(GENFIGS, "semiclassical.html"))
###```@raw html
###<object type="text/html" data="../semiclassical.html" style="width:100%;height:400px;"></object>
###```

# By [Theorem 2.20](https://arxiv.org/abs/2302.08448) it turns out that the *derivatives* of these particular semi-classical Jacobi polynomials are a linear combination of at most four polynomials orthogonal with respect to the weight $w^{(\alpha+1,\beta+1,\gamma+1,\delta+1,\epsilon+1)}(x)$ on $(-1,1)$. This fact enables us to compute the banded differentiation matrix:
v = Fun(x->(2+x)^(Œ≥+1)*(3+x)^(Œ¥+1)*(5-x)^(œµ+1), NormalizedJacobi(Œ≤+1, Œ±+1))
function threshold!(A::AbstractArray, œµ)
    for i in eachindex(A)
        if abs(A[i]) < œµ A[i] = 0 end
    end
    A
end
P‚Ä≤ = plan_modifiedjac2jac(Float64, n+1, Œ±+1, Œ≤+1, v.coefficients)
DP = UpperTriangular(diagm(1=>[sqrt(n*(n+Œ±+Œ≤+1)) for n in 1:n])) # The classical differentiation matrix representing ùíü P^{(Œ±,Œ≤)}(x) = P^{(Œ±+1,Œ≤+1)}(x) D_P.
DQ = UpperTriangular(threshold!(P‚Ä≤\(DP*(P*I)), 100eps())) # The semi-classical differentiation matrix representing ùíü Q(x) = QÃÇ(x) D_Q.
UpperTriangular(DQ[1:9, 1:9])

# A faster method now exists via the `GramMatrix` architecture and its associated displacement equation. Given the modified orthogonal polynomial moments implied by the normalized Jacobi series for $u(x)$, we pad this vector to the necessary size and construct the `GramMatrix` with these moments, the multiplication operator, and the constant $\tilde{P}_0^{(\alpha,\beta)}(x)$:
Œº = PaddedVector(u.coefficients, 2n+1)
x = Fun(x->x, NormalizedJacobi(Œ≤, Œ±))
XP2 = SymTridiagonal(Symmetric(Multiplication(x, space(x))[1:2n+1, 1:2n+1]))
p0 = Fun(NormalizedJacobi(Œ≤, Œ±), [1])(0)
G = GramMatrix(Œº, XP2, p0)
view(G, 1:7, 1:7)

# And compute its cholesky factorization. The upper-triangular Cholesky factor represents the connection between original Jacobi and semi-classical Jacobi as ${\bf P}^{(\alpha,\beta)}(x) = {\bf Q}(x) R$.
R = cholesky(G).U
UpperTriangular(view(R, 1:7, 1:7))

# Every else works almost as before, including evaluation on a Chebyshev grid:
q = k -> lmul!(P1, ldiv!(R, [zeros(k); 1.0; zeros(n-k)]))
qvals = k -> ichebyshevtransform(q(k))

# Computation of the modified Jacobi matrix:
XQ1 = FastTransforms.modified_jacobi_matrix(R, XP)
norm(XQ-XQ1)/norm(XQ)

# Plotting:
x = chebyshevpoints(Float64, n+1, Val(1))
p = plot(x, qvals(0); linewidth=2.0, legend = false, xlim=(-1,1), xlabel=L"x",
         ylabel=L"Q_n(x)", title="Semi-classical Jacobi Polynomials and Their Roots",
         extra_plot_kwargs = KW(:include_mathjax => "cdn"))
for k in 1:10
    Œª = eigvals(SymTridiagonal(XQ1.dv[1:k], XQ1.ev[1:k-1]))
    plot!(x, qvals(k); linewidth=2.0, color=palette(:default)[k+1])
    scatter!(Œª, zero(Œª); markersize=2.5, color=palette(:default)[k+1])
end
p
savefig(joinpath(GENFIGS, "semiclassical1.html"))
###```@raw html
###<object type="text/html" data="../semiclassical1.html" style="width:100%;height:400px;"></object>
###```

# And banded differentiation:
Œº‚Ä≤ = PaddedVector(v.coefficients, 2n+1)
x‚Ä≤ = Fun(x->x, NormalizedJacobi(Œ≤+1, Œ±+1))
XP‚Ä≤ = SymTridiagonal(Symmetric(Multiplication(x‚Ä≤, space(x‚Ä≤))[1:2n+1, 1:2n+1]))
p0‚Ä≤ = Fun(NormalizedJacobi(Œ≤+1, Œ±+1), [1])(0)
G‚Ä≤ = GramMatrix(Œº‚Ä≤, XP‚Ä≤, p0‚Ä≤)
R‚Ä≤ = cholesky(G‚Ä≤).U
DP = UpperTriangular(diagm(1=>[sqrt(n*(n+Œ±+Œ≤+1)) for n in 1:n])) # The classical differentiation matrix representing ùíü P^{(Œ±,Œ≤)}(x) = P^{(Œ±+1,Œ≤+1)}(x) D_P.
DQ = UpperTriangular(threshold!(R‚Ä≤*(DP*(R\I)), 100eps())) # The semi-classical differentiation matrix representing ùíü Q(x) = QÃÇ(x) D_Q.
UpperTriangular(DQ[1:9, 1:9])
